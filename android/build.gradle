
import groovy.json.JsonSlurper

def computeVersionName() {
    // dynamically retrieve version from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), "utf-8")
    return json.version
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _compileSdkVersion = safeExtGet("compileSdkVersion", 26)
def _buildToolsVersion = safeExtGet("buildToolsVersion", '26.0.3')
def _minSdkVersion = safeExtGet("minSdkVersion", 16)
def _targetSdkVersion = safeExtGet("targetSdkVersion", 26)


apply plugin: 'com.android.library'

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName computeVersionName()
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
     maven {
        url "$projectDir/../Example/node_modules/react-native/android"
    }
    maven {
        url "$projectDir/../../react-native/android"
    }
}

dependencies {
    api 'com.facebook.react:react-native:+'
    implementation 'com.google.zxing:core:3.3.2'
}
